plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'nu.studer.jooq' version '8.2'
}

group = 'com.jordansimsmith.subfootballtracker'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

sourceSets {
    main {
        java.srcDirs += '/build/generated-src/jooq'
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        targetExclude '/build/generated-src/**/*'
        googleJavaFormat().aosp()
        formatAnnotations()
    }
}

jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/subfootball_tracker'
                    user = 'user'
                    password = 'password'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = false
                        fluentSetters = false
                    }
                    target {
                        packageName = 'com.jordansimsmith.subfootball.tracker.jooq'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'com.sendgrid:sendgrid-java:4.10.2'
    implementation 'org.postgresql:postgresql:42.7.2'
    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'org.apache.curator:curator-recipes:5.6.0'
    jooqGenerator 'org.postgresql:postgresql:42.7.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.1.4'
}

test {
    useJUnitPlatform()
}
